include: "rules/common.smk"


module dna_seq_varlociraptor:
    snakefile:
        github(
            "snakemake-workflows/dna-seq-varlociraptor",
            path="workflow/Snakefile",
            tag="v3.10.1",
        )
    config:
        config


use rule * from dna_seq_varlociraptor


if not "ffpe" in dna_seq_varlociraptor.samples.columns:
    dna_seq_varlociraptor.samples["ffpe"] = 0
elif (
    dna_seq_varlociraptor.samples.loc[
        dna_seq_varlociraptor.samples["alias"] == "normal", "ffpe"
    ]
    == 1
).any():
    raise WorkflowError(
        "Invalid value for ffpe column in sample sheet. ffpe treated normal samples are not supported."
    )

for sample, events in zip(
    ["normal", "tumor"],
    ["germline,loh,loh_or_amplification", "somatic_tumor_low,somatic_tumor_high"],
):
    dna_seq_varlociraptor.samples.loc[
        dna_seq_varlociraptor.samples["alias"] == sample, "mutational_burden_events"
    ] = events
